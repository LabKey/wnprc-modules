apply plugin: 'com.moowork.node'

dependencies {

    compile project(path: "${project.parent.path}:DBUtils", configuration: "apiCompile")
    compile project("${project.parent.path}:DBUtils")
    compile project("${project.parent.path}:WebUtils")

    external "org.jooq:jooq:3.8.4"
    external "org.apache.pdfbox:pdfbox:2.0.7"

    external fileTree("${project.projectDir}/../lib")
}

sourceSets {
    main {
        java { srcDirs = [ "src/java", "gen-src/java-jooq" ]}
    }
}

task("java2ts", type: JavaExec) {

    doFirst {
        mkdir "$projectDir/build/generated-ts"
    }

    main = 'com.github.jonathonrichardson.java2ts.Compiler'
    classpath = sourceSets.main.runtimeClasspath
    args = [ "$projectDir/build/generated-ts/GeneratedFromJava.ts" ]

    inputs.dir('src/java')
    outputs.dir("$projectDir/build/generated-ts")
}

task("compileTypescript", type: Exec, dependsOn: ["java2ts", "npmInstall"]) {
    inputs.file('tsconfig.json')
    inputs.dir('src/ts')
    inputs.dir("$projectDir/build/generated-ts")
    outputs.dir("$projectDir/build/compiled-ts")

    workingDir projectDir
    commandLine "$projectDir/node_modules/.bin/tsc"
}

project.tasks.findByName("processModuleResources").dependsOn("compileTypescript")