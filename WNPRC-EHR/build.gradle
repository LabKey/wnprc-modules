apply plugin: 'com.moowork.node'

dependencies {

    compile "org.labkey:study:${project.labkeyVersion}"
    compile "org.labkey:ehr:${project.labkeyLocalVersion}:api"

    compile project(path: "${project.parent.path}:DBUtils",  configuration: "apiCompile")
    compile project("${project.parent.path}:WebUtils")

    external "joda-time:joda-time:2.9.3"
    external "org.jsoup:jsoup:1.10.3"

    external fileTree("${project.projectDir}/lib")
}

sourceSets {
    main {
        java { srcDirs = [ "src/java" ] }
    }
}

task("java2ts", type: JavaExec) {
    inputs.dir('src/java')
    outputs.dir("$projectDir/build/generated-ts")

    doFirst {
        mkdir "$projectDir/build/generated-ts"
    }

    main = 'com.github.jonathonrichardson.java2ts.Compiler'
    classpath = sourceSets.main.runtimeClasspath
    args = [ "$projectDir/build/generated-ts/GeneratedFromJava.ts" ]
}

task("compileTypescript", type: Exec, dependsOn: ["java2ts", "npmInstall"]) {
    inputs.file('tsconfig.json')
    inputs.dir('src/ts')
    inputs.dir("$projectDir/build/generated-ts")
    outputs.dir("$projectDir/build/compiled-ts")

    workingDir projectDir
    commandLine "$projectDir/node_modules/.bin/tsc"
}

project.tasks.findByName("processModuleResources").dependsOn("compileTypescript")