apply plugin: 'idea'

idea.module {
    excludeDirs += file("data")
    excludeDirs += file("logs")
}

task("buildEhrcron", group: "Docker", type: Exec, description: "Generates the ehrcron docker image") {
    workingDir "$projectDir/ehrcron"
    executable "docker"
    args "build", ".", "-t", "wnprcehr/ehrcron:v1.0.0"
}

task("buildLabkey", group: "Docker", description: "Generates the LabKey docker image") { Task t ->
    doLast {
        if (project.hasProperty('labkeyTeamcityUsername') && project.hasProperty('labkeyTeamcityPassword')) {
            t.project.exec {
                workingDir "$projectDir/labkey"
                executable "docker"
                args "build", "--build-arg", "LABKEY_TEAMCITY_USERNAME=${labkeyTeamcityUsername}", "--build-arg", "LABKEY_TEAMCITY_PASSWORD=${labkeyTeamcityPassword}", ".", "-t", "wnprcehr/labkey:17.3-financial"
            }
        } else {
            logger.warn 'To build the LabKey image from TeamCity, you must specify a username and password in your ~/.gradle/gradle.properties file.'
        }
    }
}

task("buildPostfix", group: "Docker", type: Exec, description: "Generates the postfix docker image") {
    workingDir "${project.rootDir}/docker/postfix"
    executable "docker"
    args "build", ".", "-t", "wnprcehr/postfix:latest"
}

task("build", group: "Docker", description: "Generates all the docker images in the subfolders", dependsOn: [ "buildEhrcron", "buildLabkey", "buildPostfix" ] )

task("up", group: "Docker", type: Exec, description: "Starts the docker-compose environment (labkey + postgres)") {
    workingDir "$projectDir"
    executable "docker-compose"
    args "up", "-d"
    environment CATALINA_OPTS: "-ea -Ddevmode=true"
}

task("down", group: "Docker", type: Exec, description: "Shuts down the docker-compose environment (labkey + postgres)") {
    workingDir "$projectDir"
    executable "docker-compose"
    args "down"
}
