apply plugin: 'idea'

idea.module {
    excludeDirs += file("data")
    excludeDirs += file("logs")
}

task("buildEhrcron", group: "Docker", type: Exec, description: "Generates the ehrcron docker image") {
    workingDir "$projectDir/ehrcron"
    executable "docker"
    args "build", ".", "-t", "wnprcehr/ehrcron:latest"
}

task("buildTomcat", group: "Docker", type: Exec, description: "Generates the Tomcat docker image (with R)") {
    workingDir "$projectDir/tomcat"
    executable "docker"
    args "build", ".", "-t", "wnprcehr/tomcat:22.11"
}

task("buildLabkey", group: "Docker", description: "Generates the LabKey docker image", dependsOn: ["buildTomcat", "buildEhrcron", "buildPostfix"]) { Task t ->
    doLast {
        if (hasProperty('labkeyVersion')){
            println labkeyVersion
            if (project.hasProperty('labkeyTeamcityUsername') && project.hasProperty('labkeyTeamcityPassword')) {
                t.project.exec {
                    workingDir "$projectDir/labkey"
                    executable "docker"
                    args "/hooks/build", "--build-arg", "LABKEY_TEAMCITY_USERNAME=${labkeyTeamcityUsername}", "--build-arg", "LABKEY_TEAMCITY_PASSWORD=${labkeyTeamcityPassword}", "--build-arg","LABKEY_VERSION=${labkeyVersion}", ".", "-t", "wnprcehr/labkey:22.11", "--no-cache"
                }
            } else {
                logger.warn 'To build the LabKey image from TeamCity, you must specify a username and password in your ~/.gradle/gradle.properties file.'
            }
        } else {
            logger.warn 'Must provide LabKey version via command line. (ex. ./gradlew buildLabkey -PlabkeyVersion=22.11)'
        }
    }
}

task("buildPostfix", group: "Docker", type: Exec, description: "Generates the postfix docker image") {
    workingDir "$projectDir/postfix"
    executable "docker"
    args "build", ".", "-t", "wnprcehr/postfix:latest"
}

task("build", group: "Docker", description: "Generates all the docker images in the subfolders", dependsOn: [ "buildTomcat", "buildEhrcron", "buildLabkey", "buildPostfix" ] )

task("up", group: "Docker", type: Exec, description: "Starts the docker-compose environment (labkey + postgres)") {
    workingDir "$projectDir"
    executable "docker-compose"
    args "up", "-d"
    environment CATALINA_OPTS: "-ea -Ddevmode=true"
}

task("down", group: "Docker", type: Exec, description: "Shuts down the docker-compose environment (labkey + postgres)") {
    workingDir "$projectDir"
    executable "docker-compose"
    args "down"
}
