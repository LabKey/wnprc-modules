sourceSets {
    api {
        java { srcDirs = ['api-src'] }
        output.classesDir = "${project.buildDir}/api-classes"
    }
    main {
        java { srcDirs = ['src/java', 'src/test'] }
    }
}

configurations {
    apiExternal
    apiCompile.extendsFrom(apiExternal)
    external
    compile.extendsFrom(external)
}

tasks.compileApiJava {
    options.compilerArgs << '-Werror'
}
tasks.compileJava {
    options.compilerArgs << "-Werror"
}

dependencies {
    apiCompile 'org.jetbrains:annotations:15.0'
    apiCompile project(':server:api')
    apiCompile project(":externalModules:wnprc-modules:java2ts")

    compile 'org.jetbrains:annotations:15.0'
    compile "junit:junit:${junitVersion}"
    compile project(":server:api")
    compile project(":server:internal")
    compile project(":externalModules:labModules:LDK")
    compile project(":externalModules:wnprc-modules:java2ts")
}

tasks.jar {
    manifest {
        attributes 'Sealed': 'true'
    }
}

if (project.tasks.findByName('jsp2Java') != null)
{
    Task t = project.task('moveJspOutOfJavaFolder', group: 'jsp', type: Copy,
            description: "Copy WNPRC jsp files to the ACTUAL jsp compile directory", dependsOn: ['copyJsp'])
        { CopySpec copy ->
            copy.from "${project.buildDir}/${project.jspCompile.tempDir}/webapp/java"
            copy.into "${project.buildDir}/${project.jspCompile.tempDir}/webapp"
            copy.include '**/*.jsp'
        }
    t.doFirst{ project.delete "${project.buildDir}/${project.jspCompile.tempDir}/webapp/org"  }
    t.doLast { project.delete "${project.buildDir}/${project.jspCompile.tempDir}/webapp/java" }
    project.tasks.findByName('jsp2Java').dependsOn('moveJspOutOfJavaFolder')
}

