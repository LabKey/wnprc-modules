buildscript {
    repositories {
        maven {
            url "${artifactory_contextUrl}/plugins-release"
        }
        if (gradlePluginsVersion.contains("SNAPSHOT"))
        {
            maven {
                url "${artifactory_contextUrl}/plugins-snapshot-local"
            }

        }
    }
    dependencies {
        classpath "org.labkey.build:gradlePlugins:${gradlePluginsVersion}"
    }
    configurations.all {
        // Check for updates every build for SNAPSHOT dependencies
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
}
import org.labkey.gradle.util.BuildUtils

// include all the modules in server/modules
BuildUtils.includeModules(this.settings, rootDir, [BuildUtils.SERVER_MODULES_DIR], [])

// include the modules required for base EHR
BuildUtils.includeModules(this.settings, [
         ":externalModules:labModules:GenotypeAssays"
        ,":externalModules:labModules:laboratory"
        ,":server:modules:LabDevKitModules:LDK"
        ,":externalModules:labModules:SequenceAnalysis"
        ,":externalModules:labModules:Viral_Load_Assay"
        ,":externalModules:DISCVR:OpenLdapSync"
        ,":server:modules:ehrModules:ehr"
        ,":server:modules:ehrModules:EHR_ComplianceDB"
        ,":server:modules:fcsexpress"
        ,":server:OConnorLabModules:genotyping"
        ,":server:modules:targetedms"
])

// include the WNPRC modules from the git clone
include 'externalModules:wnprc-modules:docker'
include 'externalModules:wnprc-modules:java2ts'
include 'externalModules:wnprc-modules'
file("../../").listFiles().findAll { d ->
    d.isDirectory() && (new File(d.getAbsolutePath(), 'build.gradle')).exists()
}.each { d ->
    include "externalModules:wnprc-modules:${d.getName()}"
}
